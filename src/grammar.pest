// TODO: don't parse primitive types, true, false, ... as strings, define rules for them instead
// TODO: clean this up

num = @{ int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)? }
int = { ("+" | "-")? ~ ASCII_DIGIT+ }

boolean = @{ "true" | "false" }
ident = @{ ALPHABETIC ~ (ALPHABETIC | ASCII_DIGIT | "_" )* }

string = ${ "\"" ~
    (string_part | ("\\" ~ escape_c))*
~ "\"" }
    string_part = @{ ( !("\"" | "\\" | NEWLINE) ~ ANY )+ }
    escape_c = @{ ("x" ~ ('0' .. '7') ~ HEX_DIGIT ) | ANY }

type = {
    primitive |
    opt | ptr | slice | array | arrptr | fntype |
    grouped
}
    primitive = {
        "bool" | "byte" | "u8" | "i8" | "u16" |
        "i16" | "u32" | "i32" | "float" | "unit"
    }
    opt = { "?" ~ type }
    ptr = { "*" ~ type }
    slice = { "[]" ~ type }
    array = { "[" ~ int ~ "]" ~ type }
    arrptr = { "[" ~ "*" ~ "]" ~ type }
    fntype = { "fn" ~ "(" ~ types ~ ")" ~ type }
    grouped = _{ "(" ~ type ~ ")" }

types = { (type ~ ("," ~ type)*)? ~ ","? }

type_annot_opt = { (":" ~ type)? }
type_annot = { ":" ~ type }
return_type = { type? }
typed_ident = { ident ~ type_annot }
mtyped_ident = { ident ~ type_annot_opt }

operation = _{
    concat | add | subtract | multiply | divide |
    eq | neq | lt | lte | gt | gte | cast_as
}
    add      = { "+" }
    concat   = { "++" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }
    eq       = { "==" }
    neq      = { "!=" }
    lt       = { "<" }
    lte      = { "<=" }
    gt       = { ">" }
    gte      = { ">=" }
    cast_as  = { "as" }

unop = _{ neg | not | ref | deref }
    not   = { "!" }
    ref   = { "&" }
    neg   = { "-" }
    deref = { "*" }

if = {
    "if" ~ expr ~ ":" ~ expr ~ "else" ~ expr
}

typed_idents = { (typed_ident ~ ("," ~ typed_ident)*)? ~ ","? }
mtyped_idents = { (mtyped_ident ~ ("," ~ mtyped_ident)*)? ~ ","? }
exprs = { (expr ~ ("," ~ expr)*)? ~ ","? }

lambda = {
    "fn" ~ "(" ~ mtyped_idents ~ ")" ~ return_type ~ expr
}
call = {
    ident ~ "(" ~ exprs ~ ")"
}

literal = { num | boolean | string }
expr = { term ~ (operation ~ term)*}
term = _{ if | lambda | literal | call | ident | unop ~ expr | "(" ~ expr ~ ")" | block }

binding = _{
    let_decl | fn_decl | var_decl | rebind
}
    let_decl = { "let" ~ ident ~ type_annot_opt ~ "=" ~ expr }
    fn_decl = {
        "fn" ~ ident ~ "(" ~ typed_idents ~ ")" ~ return_type
        ~ block
    }
    var_decl = { "var" ~ ident ~ type_annot_opt ~ "=" ~ expr }
    rebind = { pl_expr ~ "=" ~ expr }
return = { "ret" ~ expr }
pl_expr = { ident |
    deref_expr | array_index | field_access |
    "(" ~ pl_expr ~ ")"
}
    deref_expr = { "*" ~ expr }
    array_index = { expr ~ "[" ~ expr ~ "]" }
    field_access = { expr ~ "." ~ ident }

statement = { (binding | return | expr)? }
    stmnts = _{ (statement ~ (";" ~ statement)*)? }

block = { "{" ~ stmnts ~ "}" }
implicit_block = { stmnts }

program = _{ SOI ~
    implicit_block
~ EOI }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | &EOI) }
